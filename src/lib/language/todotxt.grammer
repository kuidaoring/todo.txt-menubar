@top ToDoList { todoContent* }

@skip { whitespace }

todoContent {
  (
    DoneContent |
    PriorityAContent |
    PriorityBContent |
    PriorityCContent |
    NormalContent
  ) NewLine
}

DoneContent {
  Done (PriorityA | PriorityB | PriorityC)? Description
}

PriorityAContent {
  PriorityA Description
}

PriorityBContent {
  PriorityB Description
}

PriorityCContent {
  PriorityC Description
}

NormalContent {
  Description
}

Description {
  (Project | Context | DueDate | URL | Content)*
}

@tokens {
  whitespace { @whitespace+ }
  NewLine { $[\n\r] }
  @precedence { NewLine, whitespace }

  Done { "x" whitespace ![\n\r]+ }
  PriorityA { "(" $[aA] ")" }
  PriorityB { "(" $[bB] ")" }
  PriorityC { "(" $[cC] ")" }

  unicodeChar { $[\u{a1}-\u{10ffff}] }
  priorityPrefix { "(" }
  projectPrefix { "+" }
  contextPrefix { "@" }
  symbolWithoutSpecialChar { $[\[\]{})'"?!*#\$%&=~^|\\`\-_;:.,/] }
  name { (@asciiLetter | @digit | unicodeChar | symbolWithoutSpecialChar)+ }
  Project { "+" (name | contextPrefix)+ }
  Context { "@" (name | projectPrefix)+ }
  URL { "http" "s"? "://" $[0-9a-zA-Z:%#\$&?!@_()~.=+\-/]+ }
  Content { name (name | priorityPrefix | contextPrefix | projectPrefix)* }
  d { $[0-9] }
  Date { d d d d "-" d d "-" d d }
  DueDate { "due:" Date }
  @precedence { whitespace, Done, DueDate, URL, Content }
}

@detectDelim